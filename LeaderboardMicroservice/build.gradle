plugins {
    id 'java'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.cloud.contract' version '4.0.0'
}

ext {
    springCloudVersion = "2025.0.0"
}

group = 'com.jzargo'
version = '0.0.1-SNAPSHOT'
description = 'LeaderboardMicroservice'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}


dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'com.jzargo:core:1.0-SNAPSHOT'

    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'

    testImplementation("org.testcontainers:testcontainers:2.0.2")
    testImplementation("org.testcontainers:testcontainers-junit-jupiter:2.0.2")
}


contracts {
    testFramework = 'JUNIT'
    baseClassForTests.set("ContractVerifierBase")
    packageWithBaseClasses.set("com.jzargo")
}

tasks.withType(Test) {
    systemProperty 'spring.profiles.active', 'contract-test'
    useJUnitPlatform()
}

publishing {
    publications {
        stubs(MavenPublication) {
            groupId = 'com.jzargo'
            artifactId = 'LeaderboardMicroservice'
            version = '0.0.1-SNAPSHOT'
            artifact(verifierStubsJar)
        }
    }
    repositories {
        mavenLocal()
    }
}
